apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'


def enablePlayRelease = System.getenv('ENABLE_PLAY_RELEASE')?.toBoolean() ?: false

def isCi = System.getenv("GITHUB_ACTIONS") == "true" || System.getenv("CI") == "true"

def myStoreFile = System.getenv('STORE_FILE')
def myStorePassword = System.getenv('STORE_PASSWORD')
def myKeyAlias = System.getenv('KEY_ALIAS')
def myKeyPassword = System.getenv('KEY_PASSWORD')

android {
    compileSdk 36
    defaultConfig {
        applicationId "org.nitri.opentopo"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 56
        versionName "1.24.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            if (myStoreFile) {
                storeFile rootProject.file(myStoreFile)
                storePassword myStorePassword
                keyAlias myKeyAlias
                keyPassword myKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
            if (myStoreFile && myStorePassword && myKeyAlias && myKeyPassword) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions = ["distribution"]

    productFlavors {
        foss {
            dimension "distribution"
            // Configuration for FOSS (GitHub, F-Droid)
            // No proprietary services, no ads
        }
        play {
            dimension "distribution"
            // Configuration for Google Play
            // Google Services, Admob
        }
    }

    // ðŸ”’ Disable unwanted variants with the modern API (AGP 8+)
    // 1) On CI: disable *all* Play variants (debug+release)
    // 2) Locally: disable Play *release* unless ENABLE_PLAY_RELEASE=true
    androidComponents {
        // CI: kill every "play" variant
        beforeVariants(selector().withFlavor("distribution", "play")) { v ->
            if (isCi) {
                v.enabled = false
            }
        }
        // Local (or non-CI): keep Play debug for testing, but block Play release unless explicitly enabled
        beforeVariants(selector().withBuildType("release").withFlavor("distribution", "play")) { v ->
            if (!isCi && !enablePlayRelease) {
                v.enabled = false
            }
        }
    }

    compileOptions {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    namespace 'org.nitri.opentopo'
    lint {
        abortOnError false
        disable 'ExpiredTargetSdkVersion', 'ExpiringTargetSdkVersion'
    }
    buildFeatures {
        buildConfig true
    }
    configurations.configureEach {
        resolutionStrategy.force 'androidx.appcompat:appcompat:1.6.1'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.4"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-runtime:2.7.2'
    implementation 'androidx.test.ext:junit:1.3.0'
    annotationProcessor 'androidx.room:room-compiler:2.7.2'
    implementation "androidx.room:room-ktx:2.7.2"
    kapt "androidx.room:room-compiler:2.7.2"
    implementation "androidx.preference:preference:1.2.1"
    implementation 'net.danlew:android.joda:2.13.1'
    implementation 'com.github.k3b:k3b-geoHelper:1.1.5'
    implementation 'com.github.ticofab:android-gpx-parser:2.3.1'
    implementation 'com.github.Pygmalion69:ors-android-client:0.1.4'
    implementation('com.squareup.picasso:picasso:2.71828') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'cardview-v7'
        exclude group: 'com.android.support', module: 'customtabs'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'exifinterface'
    }
    // OkHttp client and Picasso OkHttp3 downloader for custom headers on image requests
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    playImplementation 'com.google.android.gms:play-services-ads:24.7.0'
    playImplementation 'com.google.android.ump:user-messaging-platform:3.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}

