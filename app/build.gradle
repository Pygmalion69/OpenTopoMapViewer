apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'


def enablePlayRelease = System.getenv('ENABLE_PLAY_RELEASE')?.toBoolean() ?: false

def myStoreFile = System.getenv('STORE_FILE')
def myStorePassword = System.getenv('STORE_PASSWORD')
def myKeyAlias = System.getenv('KEY_ALIAS')
def myKeyPassword = System.getenv('KEY_PASSWORD')

android {
    compileSdk 36
    defaultConfig {
        applicationId "org.nitri.opentopo"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 53
        versionName "1.23"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            if (myStoreFile) {
                storeFile rootProject.file(myStoreFile)
                storePassword myStorePassword
                keyAlias myKeyAlias
                keyPassword myKeyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (myStoreFile && myStorePassword && myKeyAlias && myKeyPassword) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions = ["distribution"]

    productFlavors {
        foss {
            dimension "distribution"
            // Configuration for FOSS (GitHub, F-Droid)
            // No proprietary services, no ads
        }
        play {
            dimension "distribution"
            // Configuration for Google Play
            // Google Services, Admob
        }
    }

    if (!enablePlayRelease) {
        variantFilter { variant ->
            def names = variant.flavors*.name
            // To check for a certain build type, use variant.buildType.name == "<buildType>"
            if (names.contains("play") && variant.buildType.name == "release") {
                // Gradle ignores any variants that satisfy the conditions above.
                setIgnore(true)
            }
        }
    }

    compileOptions {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    namespace 'org.nitri.opentopo'
    lint {
        abortOnError false
        disable 'ExpiredTargetSdkVersion', 'ExpiringTargetSdkVersion'
    }
    buildFeatures {
        buildConfig true
    }
    configurations.configureEach {
        resolutionStrategy.force 'androidx.appcompat:appcompat:1.6.1'
    }
}

dependencies {
    implementation project(':ors-client')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.20'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.room:room-runtime:2.7.2'
    implementation 'androidx.test.ext:junit:1.2.1'
    annotationProcessor 'androidx.room:room-compiler:2.7.2'
    implementation "androidx.room:room-ktx:2.7.2"
    kapt "androidx.room:room-compiler:2.7.2"
    implementation "androidx.preference:preference:1.2.1"
    implementation 'net.danlew:android.joda:2.13.1'
    implementation 'com.github.k3b:k3b-geoHelper:1.1.5'
    implementation 'com.github.ticofab:android-gpx-parser:2.3.1'
    implementation('com.squareup.picasso:picasso:2.71828') {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'cardview-v7'
        exclude group: 'com.android.support', module: 'customtabs'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'exifinterface'
    }
    playImplementation 'com.google.android.gms:play-services-ads:24.4.0'
    playImplementation 'com.google.android.ump:user-messaging-platform:3.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

